#ifndef PUBLIC_REQUEST_IDL_761243517
#define PUBLIC_REQUEST_IDL_761243517

#include "NullableTypes.idl"

module Registry
{
module PublicRequest
{

enum ObjectType_PR
{
    contact,
    nsset,
    domain,
    keyset
};

enum ConfirmedBy
{
    SIGNED_EMAIL,
    NOTARIZED_LETTER
};

enum LockRequestType // renamed as in domain browser
{
    BLOCK_TRANSFER,
    BLOCK_TRANSFER_AND_UPDATE,
    UNBLOCK_TRANSFER,
    UNBLOCK_TRANSFER_AND_UPDATE
};

enum Language
{
    CS,
    EN
};

typedef sequence<octet> BufferValue;

struct Buffer
{
    BufferValue value;
};

exception OBJECT_NOT_FOUND {};
exception INTERNAL_SERVER_ERROR {};
exception INVALID_EMAIL {};
exception OBJECT_ALREADY_BLOCKED {};
exception OBJECT_NOT_BLOCKED {};
exception HAS_DIFFERENT_BLOCK {};
exception INVALID_PUBLIC_REQUEST_TYPE {};

interface PublicRequestIntf
{
    unsigned long long create_authinfo_request_registry_email(
        in ObjectType_PR object_type,
        in string object_handle,
        in NullableULongLong log_request_id
    )
        raises (OBJECT_NOT_FOUND, INTERNAL_SERVER_ERROR, INVALID_EMAIL);

    unsigned long long create_authinfo_request_non_registry_email(
        in ObjectType_PR object_type,
        in string object_handle,
        in NullableULongLong log_request_id,
        in ConfirmedBy confirmation_method,
        in string specified_email
    )
        raises (OBJECT_NOT_FOUND, INTERNAL_SERVER_ERROR, INVALID_EMAIL);

    unsigned long long create_block_unblock_request(
        in ObjectType_PR object_type,
        in string object_handle,
        in NullableULongLong log_request_id,
        in ConfirmedBy confirmation_method,
        in LockRequestType lock_request_type
    )
        raises (OBJECT_NOT_FOUND, INTERNAL_SERVER_ERROR, OBJECT_ALREADY_BLOCKED, OBJECT_NOT_BLOCKED, HAS_DIFFERENT_BLOCK);

    Buffer create_public_request_pdf(in unsigned long long public_request_id, in Language lang)
        raises (INTERNAL_SERVER_ERROR, INVALID_PUBLIC_REQUEST_TYPE, OBJECT_NOT_FOUND);
};

}; // PublicRequest
}; // Registry

#endif // PUBLIC_REQUEST_IDL_761243517

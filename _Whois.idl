module ccReg {

  /// Typ pro status flag domény v whoisu
  enum WhoisDomainStatus  { WHOIS_ACTIVE, WHOIS_EXPIRED };

  /// Typ chyby (výsledek klasifikace identifikátoru)
  enum WhoisErrorType {
    WE_DOMAIN_BAD_ZONE, ///< Handle je doména mimo registr
    WE_DOMAIN_LONG, ///< Handle je doména nepovolené délky (zkrácena)
    WE_INVALID, ///< Handle není validní
    WE_NOTFOUND ///< Domena nebyla nalezena
  };
 
  /// Typ pro data o doméně
  struct DomainWhois
  {
    string  fqdn; ///< Nazev domeny
    boolean enum_domain; ///< Pravda kdyz domena je enumova
    WhoisDomainStatus status; ///< Stav domény
    string created; ///< Datum vytvoření domény
    string expired; ///< Datum expirace domény
    string registrarName; ///< Jméno registrátora
    string registrarUrl; ///< Webová adresa registrátora
    NameServers ns; ///< Seznam nameserverů
    TechContact tech; ///< Seznam idenitifikátorů technických kontaktů
  };

  /// Rozhraní pro WHOIS
  interface Whois
  {
    /// Vyjímka pro případ zavazne chyby
    exception WhoisError {
      string message; ///< Popis chyby
    };
    /// Vyjímka v případě lehci chyby souvisejici z nazvem identifikatoru
    exception DomainError {
      string timestamp; ///< I když nastane chyba potřebujeme timestamp
      WhoisErrorType type; ///< Oznacuje typ chyby
    };

    /// Vraci údaje o doméně pro whois podle názvu
    /** 
     \param domain_name Název požadované domény
     \param timestamp Vrací čas vyřízení požadavku
     \return
    */
    DomainWhois getDomain(
      in string domain_name,
      out string timestamp
    ) raises (WhoisError, DomainError);
  };
};
